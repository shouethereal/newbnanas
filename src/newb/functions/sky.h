#ifndef SKY_H
#define SKY_H

#include "detection.h"
#include "noise.h"

struct nl_skycolor {
  vec3 zenith;
  vec3 horizon;
  vec3 horizonEdge;
};

// rainbow spectrum
vec3 spectrum(float x) {
  vec3 s = vec3(x-0.5, x, x+0.5);
  s = smoothstep(1.0,0.0,abs(s));
  return s*s;
}

vec3 getUnderwaterCol(vec3 FOG_COLOR) {
  return 2.0*NL_UNDERWATER_TINT*FOG_COLOR*FOG_COLOR;
}

vec3 getEndZenithCol() {
  return NL_END_ZENITH_COL;
}

vec3 getEndHorizonCol() {
  return NL_END_HORIZON_COL;
}

nl_skycolor nlEndSkyColors(nl_environment env) {
  nl_skycolor s;
  s.zenith = getEndZenithCol();
  s.horizon = getEndHorizonCol();
  s.horizonEdge = s.horizon;
  return s;
}

nl_skycolor nlOverworldSkyColors(nl_environment env) {
  nl_skycolor s;
  float f = 1.0 + 2.0*(1.0-max(-env.dayFactor, 0.0));
  float nightFactor = step(env.dayFactor, 0.0);
  s.zenith = mix(NL_DAY_ZENITH_COL, NL_NIGHT_ZENITH_COL*f, nightFactor);
  s.horizon = mix(NL_DAY_HORIZON_COL, NL_NIGHT_HORIZON_COL*f, nightFactor);
  s.horizonEdge = mix(NL_DAY_EDGE_COL, NL_NIGHT_EDGE_COL*f, nightFactor);

  float dawnFactor = 1.0-env.dayFactor*env.dayFactor;
  dawnFactor *= dawnFactor*dawnFactor;
  dawnFactor *= mix(1.0, dawnFactor*dawnFactor, nightFactor);
  s.zenith = mix(s.zenith, NL_DAWN_ZENITH_COL, dawnFactor);
  s.horizon = mix(s.horizon, NL_DAWN_HORIZON_COL, dawnFactor);
  s.horizonEdge = mix(s.horizonEdge, NL_DAWN_EDGE_COL, dawnFactor);

  float zh = dot(s.zenith, vec3_splat(0.33));
  float hh = dot(s.horizon, vec3_splat(0.33));
  float rainMix = env.rainFactor*NL_SKY_RAIN_MIX_FACTOR;
  s.zenith = mix(s.zenith, NL_RAIN_ZENITH_COL*zh, rainMix);
  s.horizon = mix(s.horizon, NL_RAIN_HORIZON_COL*hh, rainMix);
  s.horizonEdge = mix(s.horizonEdge, s.horizon, env.rainFactor);

  if (env.underwater) {
    vec3 underwaterFog = env.fogCol*env.fogCol*NL_UNDERWATER_TINT;
    s.zenith = mix(2.0*underwaterFog, underwaterFog*zh, 0.8);
    s.horizon = mix(2.0*underwaterFog, underwaterFog*hh, 0.8);
    s.horizonEdge = s.horizon;
  }

  return s;
}

nl_skycolor nlSkyColors(nl_environment env) {
  if (env.end) {
    return nlEndSkyColors(env);
  }
  return nlOverworldSkyColors(env);
}


vec3 renderOverworldSky(nl_skycolor skyCol, nl_environment env, vec3 viewDir, bool isSkyPlane) {
  float avy = abs(viewDir.y);
  float mask = 0.5 + (0.5*viewDir.y/(0.4 + avy));

  vec2 g = clamp(0.5 - 0.5*vec2(dot(env.sunDir, viewDir), dot(env.moonDir, viewDir)), 0.0, 1.0);
  vec2 g1 = 1.0-mix(sqrt(g), g, env.rainFactor);
  vec2 g2 = g1*g1;
  vec2 g4 = g2*g2;
  vec2 g8 = g4*g4;
  float mg8 = (g8.x+g8.y)*mask*(1.0-0.9*env.rainFactor);

  float vh = 1.0 - viewDir.y*viewDir.y;
  float vh2 = vh*vh;
  vh2 = mix(vh2, mix(1.0, vh2*vh2, NL_SKY_VOID_FACTOR), step(viewDir.y, 0.0));
  vh2 = mix(vh2, 1.0, mg8);
  float vh4 = vh2*vh2;

  float gradient1 = vh4*vh4;
  float gradient2 = 0.8*gradient1 + 0.2*vh2;
  gradient1 *= gradient1;
  gradient1 = mix(gradient1*gradient1, 1.0, mg8);
  gradient2 = mix(gradient2, 1.0, mg8);

  float dawnFactor = 1.0-env.dayFactor*env.dayFactor;
  float df = mix(1.0, g2.x, dawnFactor*dawnFactor);
  vec3 sky = mix(skyCol.horizon, skyCol.horizonEdge, gradient1*df*df);
  sky = mix(skyCol.zenith, sky, gradient2*df);

  sky *= 0.5+0.5*gradient2;
  sky *= (1.0 + (2.0*mg8 + 7.0*mg8*mg8)*mask)*mix(1.0, mask, NL_SKY_VOID_DARKNESS);

  if (!isSkyPlane) {
    float source = max(0.0, (mg8-0.22)/0.78);
    source *= source;
    source *= source;
    sky *= 1.0 + 15.0*source*(1.0-env.rainFactor);
  }

  #ifdef NL_RAINBOW
    float rainbowFade = 0.5 + 0.5*viewDir.y;
    rainbowFade *= rainbowFade;
    rainbowFade *= mix(NL_RAINBOW_CLEAR, NL_RAINBOW_RAIN, env.rainFactor);
    rainbowFade *= 0.5+0.5*env.dayFactor;
    sky += spectrum(24.2*(0.85-g.x))*rainbowFade*skyCol.horizon;
  #endif

  return sky;
}

vec3 renderEndSky(nl_skycolor skycol, vec3 viewDir, float t){
  vec3 sky = vec3_splat(0.0);
  viewDir.y = smoothstep(-1.2,1.5,abs(viewDir.y));
  viewDir.x += 0.004*sin(10.0*viewDir.y - t + viewDir.z);

  float a = atan2(viewDir.x, viewDir.z);

  float s = sin(a*6.0 + 0.5*t);
  s = s*s;
  s *= 0.0 + 0.6*sin(a*11.0 - 0.22*t);
  float g = smoothstep(1.8-s, -1.2, viewDir.y);

  float f = (0.5*g + 1.5*smoothstep(1.2,-0.2,viewDir.y));
  float h = (1.2*g + 0.8*smoothstep(1.2,-0.4,viewDir.y));

  sky += mix(skycol.zenith, mix(skycol.horizon, mix(skycol.horizon, NL_END_STREAK, 0.35), g), f*f);
  sky += mix(skycol.horizon, mix(skycol.horizon, NL_END_STREAK, 0.35), g)*(g*g*g*g*0.6 + 0.4*h*h*h*h);

  return sky;
}

// Blackhole center for manual control (don't change)
#define NL_BH_CENTER_X 0.7
#define NL_BH_CENTER_Y 0.25
#define NL_BH_CENTER_Z 0.6

vec4 renderBlackhole(vec3 vdir, float t) {
  t *= NL_BH_SPEED;

  float r = NL_BH_DIR;
  //r += 0.0001 * t;
  vec3 vr = vdir;

  //vr.xy = mat2(cos(r), -sin(r), sin(r), cos(r)) * vr.xy;
  // manual calculation mat2 to fix windows compiling
  float cx = cos(r);
  float sx = sin(r);
  vr.xy = vec2(cx*vr.x - sx*vr.y, sx*vr.x + cx*vr.y);
  //r *= 2.0;

  vec3 bhCenter = vec3(NL_BH_CENTER_X, NL_BH_CENTER_Y, NL_BH_CENTER_Z);
  bhCenter.xy = vec2(cx*bhCenter.x - sx*bhCenter.y, sx*bhCenter.x + cx*bhCenter.y);

  vec3 vd = vr - bhCenter;
    
  float nl = sin(8.0*vd.x + t)*sin(8.0*vd.y - t)*sin(8.0*vd.z + t);
  nl = mix(nl, sin(4.0*vd.x + t)*sin(4.0*vd.y - t), 0.5);

  float a = atan2(vd.x, vd.z);
  float d = NL_BH_DIST*length(vd + 0.002*nl);

  float d0 = (0.6 - d) / 0.6;
  float dm0 = 1.0 - max(d0, 0.0);
    
  float gl = 1.0 - clamp(-0.2*d0, 0.0, 1.0);
  float gla = pow(1.0 - min(abs(d0), 1.0), 6.0);
  float gl8 = pow(gl, 6.0); 

  float hole = 0.9*pow(dm0, 20.0) + 0.1*pow(dm0, 3.0);
  float bh = (gla + 0.7*gl8 + 0.2*gl8*gl8)*hole;

  float df = sin(2.0*a - 3.0*d + 20.0*pow(1.2 - d, 3.0) + t*0.5);
  df *= 0.85 + 0.1*sin(6.0*a + d + 2.0*t - 2.0*df);
  bh *= 1.0 + pow(df, 3.0)*hole*max(1.0 - bh, 0.0);

  vec3 col = bh*3.5*mix(NL_BH_COL_LOW, NL_BH_COL_HIGH, smoothstep(0.0, 1.0, bh));
  return vec4(col, hole);
}

vec3 distortByBlackhole(vec3 vdir, float t, float strength) {
  // Rotation of viewdir and blackhole center to the same space
  float r = NL_BH_DIR;
  float cx = cos(r);
  float sx = sin(r);

  // Blackhole center after rotation
  vec3 bhCenter = vec3(NL_BH_CENTER_X, NL_BH_CENTER_Y, NL_BH_CENTER_Z);
  bhCenter.xy = vec2(cx*bhCenter.x - sx*bhCenter.y, sx*bhCenter.x + cx*bhCenter.y);

  // Viewdir is also rotated to blackhole space
  vec3 vdir_rot = vdir;
  vdir_rot.xy = vec2(cx*vdir.x - sx*vdir.y, sx*vdir.x + cx*vdir.y);

  vec3 toBH = bhCenter - vdir_rot;
  float dist = length(toBH);
  float effect = smoothstep(0.6, 0.4, dist);
  vec3 dir = normalize(vdir_rot - bhCenter);
  float bend = strength*effect/(dist+0.2);

  vdir_rot = normalize(mix(vdir_rot, dir, bend));
  // Inverse rotation to return to the original viewdir space
  float icx = cos(-r);
  float isx = sin(-r);
  vdir_rot.xy = vec2(icx*vdir_rot.x - isx*vdir_rot.y, isx*vdir_rot.x + icx*vdir_rot.y);

  return vdir_rot;
}

vec3 nlRenderSky(nl_skycolor skycol, nl_environment env, vec3 viewDir, float t, bool isSkyPlane) {
  vec3 sky;
  viewDir.y = -viewDir.y;

  if (env.end) {
    sky = renderEndSky(skycol.horizon, skycol.zenith, viewDir, t);
  } else {
    sky = renderOverworldSky(skycol, env, viewDir, isSkyPlane);
    #ifdef NL_UNDERWATER_STREAKS
      // if (env.underwater) {
      //   float a = atan2(viewDir.x, viewDir.z);
      //   float grad = 0.5 + 0.5*viewDir.y;
      //   grad *= grad;
      //   float spread = (0.5 + 0.5*sin(3.0*a + 0.2*t + 2.0*sin(5.0*a - 0.4*t)));
      //   spread *= (0.5 + 0.5*sin(3.0*a - sin(0.5*t)))*grad;
      //   spread += (1.0-spread)*grad;
      //   float streaks = spread*spread;
      //   streaks *= streaks;
      //   streaks = (spread + 3.0*grad*grad + 4.0*streaks*streaks);
      //   sky += 2.0*streaks*skycol.horizon;
      // }
    #endif
  }

  return sky;
}

// shooting star
vec3 nlRenderShootingStar(vec3 viewDir, vec3 FOG_COLOR, float t) {
  // transition vars
  float h = t / (NL_SHOOTING_STAR_DELAY + NL_SHOOTING_STAR_PERIOD);
  float h0 = floor(h);
  t = (NL_SHOOTING_STAR_DELAY + NL_SHOOTING_STAR_PERIOD) * (h-h0);
  t = min(t/NL_SHOOTING_STAR_PERIOD, 1.0);
  float t0 = t*t;
  float t1 = 1.0-t0;
  t1 *= t1; t1 *= t1; t1 *= t1;

  // randomize size, rotation, add motion, add skew
  float r = fract(sin(h0) * 43758.545313);
  float a = 6.2831*r;
  float cosa = cos(a);
  float sina = sin(a);
  vec2 uv = viewDir.xz * (6.0 + 4.0*r);
  uv = vec2(cosa*uv.x + sina*uv.y, -sina*uv.x + cosa*uv.y);
  uv.x += t1 - t;
  uv.x -= 2.0*r + 3.5;
  uv.y += viewDir.y * 3.0;

  // draw star
  float g = 1.0-min(abs((uv.x-0.95))*20.0, 1.0); // source glow
  float s = 1.0-min(abs(8.0*uv.y), 1.0); // line
  s *= s*s*smoothstep(-1.0+1.96*t1, 0.98-t, uv.x); // decay tail
  s *= s*s*smoothstep(1.0, 0.98-t0, uv.x); // decay source
  s *= 1.0-t1; // fade in
  s *= 1.0-t0; // fade out
  s *= 0.7 + 16.0*g*g;
  s *= max(1.0-FOG_COLOR.r-FOG_COLOR.g-FOG_COLOR.b, 0.0); // fade out during day
  return s*vec3(0.8, 0.9, 1.0);
}

// Galaxy stars - needs further optimization
vec3 nlRenderGalaxy(vec3 vdir, vec3 fogColor, nl_environment env, float t) {
  #ifdef GALAXY_STARS
  #else
    if (env.underwater) {
      return vec3_splat(0.0);
    }
  #endif

  t *= NL_GALAXY_SPEED;

  // rotate space
  float cosb = sin(0.2*t);
  float sinb = cos(0.2*t);
  vdir.xy = mul(mat2(cosb, sinb, -sinb, cosb), vdir.xy);
  //vdir.xy = mat2(cosb, sinb, -sinb, cosb) * vdir.xy;

  // noise
  float n0 = 0.5 + 0.5*sin(5.0*vdir.x)*sin(5.0*vdir.y - 0.5*t)*sin(5.0*vdir.z + 0.5*t);
  float n1 = noise3D(15.0*vdir + sin(0.85*t + 1.3));
  float n2 = noise3D(50.0*vdir + 1.0*n1 + sin(0.7*t + 1.0));
  float n3 = noise3D(200.0*vdir - 10.0*sin(0.4*t + 0.500));

  // stars
  n3 = smoothstep(0.04,0.3,n3+0.02*n2);
  float gd = vdir.x + 0.1*vdir.y + 0.1*sin(10.0*vdir.z + 0.2*t);
  float st = n1*n2*n3*n3*(1.0+70.0*gd*gd);
  st = (1.0-st)/(1.0+400.0*st);
  if (env.end) {
    float gf = 1.0 - (vdir.x*vdir.x + 0.03*n1 + 0.2*n0);
    vec3 stars = mix(vec3(1.2, 1.0, 0.8), NL_END_STREAK*3.5, gf);
    return vec3_splat(st) + st*gf*2.2*stars;
  }
  vec3 stars = (0.8 + 0.2*sin(vec3(8.0,6.0,10.0)*(2.0*n1+0.8*n2) + vec3(0.0,0.4,0.82)))*st;

  // glow
  float gfmask = abs(vdir.x)-0.15*n1+0.04*n2+0.25*n0;
  float gf = 1.0 - (vdir.x*vdir.x + 0.03*n1 + 0.2*n0);
  gf *= gf;
  gf *= gf*gf;
  gf *= 1.0-0.3*smoothstep(0.2, 0.3, gfmask);
  gf *= 1.0-0.2*smoothstep(0.3, 0.4, gfmask);
  gf *= 1.0-0.1*smoothstep(0.2, 0.1, gfmask);
  vec3 gfcol = normalize(vec3(n0, cos(2.0*vdir.y), sin(vdir.x+n0)));
  stars += (0.4*gf + 0.012)*mix(vec3(0.5, 0.5, 0.5), gfcol*gfcol, NL_GALAXY_VIBRANCE);

  stars *= mix(1.0, NL_GALAXY_DAY_VISIBILITY, min(dot(fogColor, vec3(0.5,0.7,0.5)), 1.0)); // maybe add day factor to env for global use?

  return stars*(1.0-env.rainFactor);
}


#endif
